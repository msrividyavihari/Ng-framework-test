plugins {
	id 'java-library'
	id 'eclipse'
	id 'idea'
	id 'io.freefair.lombok' version '5.2.1'
	id "com.diffplug.eclipse.apt" version "3.25.0"
	id 'maven-publish'
}

apply from: "${rootProject.projectDir}/gradle/sonarqube.gradle"

allprojects {
	group 'com.deloitte.nextgen.data-collection'
	version '1.0-SNAPSHOT'
	generateLombokConfig.enabled = false
	eclipse {
		classpath {
			downloadJavadoc = true
			downloadSources = true
			defaultOutputDir = file("build/default")
			file.whenMerged {
				entries.each { entry ->
					if (entry.kind == 'src' && entry.hasProperty('output')) {
						entry.output = entry.output.replace('bin/', "build/")
					}
				}
			}
		}
	}

	idea {
		module {
			downloadJavadoc = true
			downloadSources = true
		}
	}

	java{
		sourceCompatibility = JavaVersion.VERSION_1_8
		targetCompatibility = JavaVersion.VERSION_1_8
		withSourcesJar()
	}
	
	repositories {
		mavenLocal()
		mavenCentral()
		//gradlePluginPortal()
		maven { url 'https://repo.spring.io/milestone' }
		maven { url 'https://repo.spring.io/snapshot' }
		maven { url 'https://repo.spring.io/plugins-release'}
		//maven {url "https://plugins.gradle.org/m2/"}
		maven { url 'http://fast4j.glbsnet.com:8086/artifactory/repo' }
	}
}

subprojects {

	apply plugin: 'java-library'
    apply plugin: 'maven'
    apply plugin: 'maven-publish'
    apply plugin: 'io.freefair.lombok'
	
	dependencies {

		// This should be added before Lombok plugin ..
		// hence adding this  dependency here
		annotationProcessor "org.hibernate.orm:hibernate-jpamodelgen:6.0.0.Alpha7"
		implementation "javax.validation:validation-api:2.0.1.Final"
		implementation 'com.google.guava:guava:30.1.1-jre'

		testImplementation(platform('org.junit:junit-bom:5.7.0'))
		testImplementation('org.junit.jupiter:junit-jupiter')

		testImplementation 'org.assertj:assertj-core:3.18.0'
		testImplementation 'org.assertj:assertj-guava:3.4.0'

		testImplementation 'org.hamcrest:hamcrest-all:1.3'
		testImplementation 'org.mockito:mockito-all:1.10.19'
		testImplementation 'com.jayway.jsonpath:json-path-assert:2.4.0'

		testImplementation 'org.jeasy:easy-random-core:4.2.0'
		testImplementation 'io.github.glytching:junit-extensions:2.4.0'
	}
	
	configurations {
		compileOnly {
			extendsFrom annotationProcessor
		}
	}
	
	tasks.withType(JavaCompile) {
		options.encoding = 'UTF-8'
	}

	test {
		useJUnitPlatform()
		testLogging {
			events "passed", "skipped", "failed"
		}
	}

	task sourcesJar(type: Jar, dependsOn: classes) {
		classifier = 'sources'
		from sourceSets.main.allSource
	}

	task javadocJar(type: Jar, dependsOn: javadoc) {
		classifier = 'javadoc'
		from javadoc.destinationDir
	}

	artifacts {
		archives sourcesJar
		archives javadocJar
	}
}


